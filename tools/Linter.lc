<?livecode

local sErrorsList
local sLastLine

Lint

command Lint
  local tScope
  repeat for each element theArgument in the commandArguments
    if theArgument begins with "-scope" then
       split theArgument with "="
       put theArgument[2] into tScope
    end if
  end repeat

  read from stdin until empty
  put it into tScript

  put the scriptParsingErrors into sErrorsList
  split sErrorsList with return

  create script only stack "TestScript"

  if tScope is empty or tScope is ".source.livecodescript" then
    local tLineOffset = 0

    -- check for script only
    if word 1 of tScript is "script" then
      put 1 into tLineOffset
    end if

    if tLineOffset is 1 then
      delete line 1 of tScript
    end if

    set the script of stack "TestScript" to tScript

    put the result into theErrors
    split theErrors with return
    repeat with tIndex = 1 to the number of elements in theErrors
      if theErrors[tIndex] is not empty then
        put sErrorsList[item 1 of theErrors[tIndex]] into tMessage
        if tMessage is not empty then
          if item 4 of theErrors[tIndex] is not empty then
            put " (" & item 4 of theErrors[tIndex] & ")" after tMessage
          end if
          write item 2 of theErrors[tIndex] + tLineOffset, item 3 of theErrors[tIndex], tMessage & linefeed to stdout
        end if
      end if
    end repeat
  else if tScope is ".source.iRev" then
    -- write out to a temporary file and include
    put the temporary folder & slash & uuid() into tFile
    -- can't lint a whole web app...
    replace "include" with "# include" in tScript
    replace "require" with "# require" in tScript
    -- ensure it throws an error so it's not exectuted
    put return & quote after tScript
    put the number of lines of tScript into sLastLine
    put tScript into url ("binfile:" & tFile)
    include tFile
  end if

  write linefeed to stdout

end Lint

command scriptExecutionError pStack, pFiles
  local tHint, tFile
  --write 1,1,line 2 of pStack & linefeed to stdout
  split pStack with return
  repeat with tIndex = 2 to the number of elements in pStack
    if item 1 of pStack[tIndex] is 730 then
      exit repeat
    end if
    if item 2 of pStack[tIndex] is not 0 and item 2 of pStack[tIndex] is not sLastLine then
      put sErrorsList[item 1 of pStack[tIndex]] into tMessage
      if tMessage is not empty then
        if item 4 of pStack[tIndex] is not empty then
          put " (" & item 4 of pStack[tIndex] & ")" after tMessage
        end if
        write item 2 of pStack[tIndex], item 3 of pStack[tIndex], tMessage & linefeed to stdout
      end if
    end if
  end repeat

  -- cleanup
  set the itemDelimiter to slash
  repeat for each line tFile in pFiles
    if item -1 of tFile is "Linter.lc" then
      next repeat
    end if
    delete file tFile
  end repeat

  write linefeed to stdout
end scriptExecutionError
